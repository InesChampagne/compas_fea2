!-------------------------------------------------------------------------------
! 
! Benachmarks: Bathe Beam with Ansys MPADL
! 
! Programmierung:   Marius Weber
!
! Datum:            05.03.2021
!
!-------------------------------------------------------------------------------
! Clear & Start New
!-------------------------------------------------------------------------------
finish    
/clear

!-------------------------------------------------------------------------------
! Material properties
!-------------------------------------------------------------------------------

! Predefine E-Modulus and poissons ratio - Can later (in CompasFEA) be written directly to the desired location -> Material
Ec  = 200000
vc= 0.3


!-------------------------------------------------------------------------------
! Geometry
!-------------------------------------------------------------------------------

!	Enters the model creation preprocessor
/prep7      

! Definition of the Keypoints (k,number of the keypoint,X,Y,Z) 
! The number of the keypoint could also be empty and Ansys will do the numbering itself (in ascending order)
k,1,0,0,0  
k,2,20000,0,0
k,3,20000,1000,0
k,4,0,1000,0

! Defines an area by connecting keypoints (a,KP1,KP2,KP3,KP4,...,...,...,KP18) 
! The numbering can be clockwise or counterclockwise. 
! But it has an influence on the automatic definition of the local coordinate system. 
! However, since we still define the local coordiante system ourselves, 
! it usually doesn't matter if we numbering clockwise or counterclockwise.
a,1,2,3,4

! Predefine the numbers of layers for Shell181 - Can later (in CompasFEA) be written directly to the desired location -> Element type
nn=20

! Predefine the thickness of the beam - Can later (in CompasFEA) be written directly to the desired location -> Element type
h=100

!-------------------------------------------------------------------------------
! Element type
!-------------------------------------------------------------------------------

! Predefine the stiffnesses for Shell181 element (based on the Reissner/Mindlin plate theory)               
G   = Ec/(2*(1+vc))             ! shear modulus
delta_h = h/nn	                ! thickness of one layer																																
h_shear = h                     ! thickness of the shell    
faktor = 1                      ! user defined factor 
kk  = 5/6                       ! shear correction factor
E111  =  kk*G*h_shear*faktor    ! Transverse shear stiffness 
E221  =  kk*G*h_shear*faktor    ! Transverse shear stiffness                                
E121  =  0                      ! Transverse shear stiffness 

! Define a local element type and its keyoptions from the element library                    
et,1,shell181                 ! Definition of SHELL181 (et, local element type number, element name as given in the library)  
keyopt,1,1,0                  ! Keyoption, Element typ 1, Keyoption 1, Value 0 (0=Bending and membrane stiffness) 
keyopt,1,3,2                  ! Keyoption, Element typ 1, Keyoption 3, Value 2 (2=Full integration with incompatible modes) 
keyopt,1,8,2                  ! Keyoption, Element typ 1, Keyoption 8, Value 2 (2=Store data for TOP, BOTTOM, and MID for all layers; applies to single- and multi-layer elements)

! Section-Type
sectype,1,shell                        ! Associates section type information with a section ID number (sectype,SecID, Element Type, Name)
seccontrols,E111,E221,E121,,1,1,1      ! Supplements or overrides default section properties (seccontrols,E111, E221, E121, ADDMAS, MEMSCF, BENSCF, Drillstiff)
secdata,delta_h,1,0,1,,                ! Describes the geometry of a section (secdata,thickness of one layer, material number, theta, number of integration points in layer)
   
!------------------------------------------------------------------------------- 
! Elementattributs 
!------------------------------------------------------------------------------- 
aatt,1,1,1,,1   ! Associates element attributes with the selected, unmeshed areas (AATT, Material number, real constant, Element typ, ESYS, SECN) 
asel,all        ! Select all areas 

!-------------------------------------------------------------------------------
! Material
!------------------------------------------------------------------------------- 
mp,dens,1,0.0000025     ! Defines a linear material (mp,mass denstiy, Material number, value)
mp,ex,1,Ec              ! Defines a linear material (mp,elastic moduli, Material number, value)
mp,prxy,1,vc            ! Defines a linear material (mp,poisson's ratios, Material number, value)

!-------------------------------------------------------------------------------
! Meshing
!------------------------------------------------------------------------------- 
eg = 250                ! predefine element size
lesize,all,eg           ! Specifies the divisions and spacing ratio on unmeshed lines (lesize,all lines,size)
mshkey,2                ! Specifies whether free meshing or mapped meshing should be used to mesh a model (2=use mapped meshing if possible)
mshape,0,2D             ! Specifies the element shape (0=use quadtrilateral-shaped elements)
amesh,all               ! Generates nodes and area elements within the selected areas (here all
allsel                  ! Select all areas 

!-------------------------------------------------------------------------------
! local coordiante system
!-------------------------------------------------------------------------------
cskp,11,0,1,2,4		 	! Defines a local coordinate system by three keypoint locations (cskp, reference number of the LC, Cartesian, origin, KP_x, KP_y)
emodif,all,esys,11      ! Modifies a previously defined element 
allsel                  ! Select all areas 
CSYS,0                  ! Activates a previously defined coordinate system (0=Cartesian)

!-------------------------------------------------------------------------------
! Constraints
!------------------------------------------------------------------------------- 
dl,4,,all,0             ! Defines DOF constraints on lines (dl,Line number, , all degress, value)

!-------------------------------------------------------------------------------
! Loads
!------------------------------------------------------------------------------- 
F,82,FY,-100            ! Specifies force loads at nodes (F, Node Number, Force in y-direction (refer to global CS), Value)

!-------------------------------------------------------------------------------	    
! Solution 
!-------------------------------------------------------------------------------
/solu
! Sets convergence values in case of a nonlinear analyses 
CNVTOL,F,,0.9     
CNVTOL,U,,0.9
CNVTOL,m,-1,3

! Solver-settings
autots,1                  ! Specifies automatic time stepping (1=On) 
nsubst,1,1,1              ! Specifies the number of substeps to be taken this load step(nsubst,first,max,min)
time,1                    ! Sets the time for a load step (1=time one)

! Control the solution data written to the database
outres,erase              ! esets OUTRES specifications to their default values
outres,nsol,all           ! Nodal DOF solution (all=for all substeps)
outres,rsol,all           ! Nodal reaction solution (all=for all substeps)
outres,esol,all           ! Element solution (all=for all substeps)
outres,svar,all           ! State variables (used by USERMAT) (all=for all substeps)
                          
! Solver
solve                     ! Starts the solution  

!-------------------------------------------------------------------------------	    
! Outputs 
!-------------------------------------------------------------------------------

! Enter postprocessor
/POST1                              

esel,all        ! select all elements
*GET,Sol_,ACTIVE,0,SOLU,CNVG        ! "CNVG=0" : Not converged


! Extract the coordinates of each node AND the nodes correspond. 
! The following principle can be used for all outputs (standard and user defined). 
! The output corresponds to the calculated results at the GP and not (!) to the interpolated results at a node 

! Coordinates of the nodes 'coor_nodes.txt'
*GET,NrN,NODE,0,COUNT               ! NrN: Number of nodes in the list
*DIM,N_N,ARRAY,NrN,1
*VGET,N_N,NODE, ,NLIST              ! N_N: Node list array
*DIM,coor_nodes,ARRAY,NrN,4
    *DO,ii,1,NrN
      coor_nodes(ii,1) = N_N(ii)                ! 1. node number
      *GET,coor_nodes(ii,2),NODE,N_N(ii),LOC,X  ! 2. x-coor of node
      *GET,coor_nodes(ii,3),NODE,N_N(ii),LOC,Y  ! 3. y-coor of node
      *GET,coor_nodes(ii,4),NODE,N_N(ii),LOC,Z  ! 4. z-coor of node     
    *ENDDO    
        
 
! Macro for coor_nodes - txt
! -------------------------------------------------------------

! Arg1 is the array name (string)
! Arg2 is the format descriptor (string)
! Arg3 is the number of columns to write (integer) 
! Arg4 is the file name and extention (string)

Arg1 = 'coor_nodes'
Arg2 = 'f20.5'
Arg3 = 4
Arg4 = 'coor_nodes.txt'

    ~eui,'set arrname [ans_getvalue PARM,Arg1,value]'
    ~eui,'set formatdescriptor [ans_getvalue PARM,Arg2,value]'
    ~eui,'set numcol [ans_getvalue PARM,Arg3,value]'
    ~eui,'set filename [ans_getvalue PARM,Arg4,value]'
    ! Trim up the strings a bit
    ~eui,'set arrname [string trim $arrname]'
    ~eui,'set formatdescriptor [string trim $formatdescriptor]'
    ~eui,'set filename [string trim $filename]' 
    ~eui,'set cmd "*mwrite,$arrname"'
    ~eui,'append cmd "(1,1),"'
    ~eui,'append cmd [file rootname $filename]'
    ! Put a comma between the filename and extension
    ~eui,'append cmd ","'
    ! Pull off the leading period from the extension. Leaving off the
    ! comma above and forgeting to strip off the leading period in the
    ! extension is a case of two errors canceling each other out. If 
    ! only life were always so kind...
    ~eui,'append cmd [string trimleft [file extension $filename] "."]'
    ~eui,'append cmd "\n"'
    ~eui,'append cmd "($numcol$formatdescriptor)"
    ~eui,'set fid [open mk_mwritehelper.mac w]'
    ~eui,'puts $fid $cmd'
    ~eui,'close $fid'
    ~eui,'ans_sendcommand mk_mwritehelper'
    ~eui,'file delete mk_mwritehelper.mac'
                                     
